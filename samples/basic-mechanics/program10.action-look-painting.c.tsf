;TODO: check if player is already in point 11

; first go to the place the door is
MoveCharToPos 0,11,0
;TODO: lock walk path
;wait for key char 0 of type 1 with ID 11 (for character 0's movement until moved to point 11)
SetupWaitingChar 0,1,11

;skip saying the first line if already known about
Push
GetFlag 405
Push
FetchScriptWord 1
TestEquals
Jnz :skip-first-talk-line
;say the text lines
StartTalk 0,7
;wait for key char 0 of type -1 with IDs 0 (for character 0's animation to be 0 (idle, not speaking))
SetupWaitingChar 0,-1,0
;make sure the non-temporary flag exists to mark the hitbox as having had its text updated, main program will check
Push
FetchScriptWord 1
SetFlag 405
SetHitboxText 4
SetCharDelay 5

skip-first-talk-line:
;say the text line
StartTalk 0,8
;wait for key char 0 of type -1 with IDs 0 (for character 0's animation to be 0 (idle, not speaking))
SetupWaitingChar 0,-1,0
;extra delay to be more natural
SetCharDelay 5

;load third room without changing program to show a 'pop-up'
;disable room scroll/inventory menu for it to work right
Push
FetchScriptWord 1
;disable room scroll
SetFlag 616
;disable inventory menu
SetFlag 606
FetchScriptWord 0
;set room x to 0
SetFlag 614
;set room y to 0
SetFlag 615
;disable input
EnableInput

;actually load the room
LoadRoom 3
;NOTE: the indexes after this (text, point) are from room 3!
;hide and move characters to prevent them showing up on top of the 'pop-up'
;teleport character
SetCharBox 0,1
;make character be hidden on screen
SetCharFlags 0,32768



;say the text line
StartTalk 0,1
;wait for key char 0 of type -1 with IDs 0 (for character 0's animation to be 0 (idle, not speaking))
SetupWaitingChar 0,-1,0
;extra delay to be more natural
SetCharDelay 10

;say the text line
StartTalk 0,2
;wait for key char 0 of type -1 with IDs 0 (for character 0's animation to be 0 (idle, not speaking))
SetupWaitingChar 0,-1,0
;extra delay to be more natural
SetCharDelay 10

;say the text line
StartTalk 0,3
;wait for key char 0 of type -1 with IDs 0 (for character 0's animation to be 0 (idle, not speaking))
SetupWaitingChar 0,-1,0
;extra delay to be more natural
SetCharDelay 10



;load the previous room and move everything back
LoadRoom 2
;NOTE: the indexes after this (text, point) are from room 2 again!

;re-enable room scroll/inventory menu
Push
FetchScriptWord 0
SetFlag 616
SetFlag 606
;deliberately re-draw the inventory to make it correctly re-render
DrawInventory 0
;enable input
DisableInput

;teleport character
SetCharBox 0,11
;make character be shown on screen
UnsetCharFlags 0,32768

;end
StopScript