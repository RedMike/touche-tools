Noop

;    initial setup
;show inventory
FetchScriptWord 0
SetFlag 606

;hide cursor
FetchScriptWord 1
SetFlag 618
;    initial setup done





;    load data and setup key characters
;load sprites (index 0 is now num 1) and animations (index 0 is now num 1)
LoadSprite 0,1
LoadSequence 0,1

;set up main key character (0) with sprite index 0 (ties to num 1, so char), animation index 0 (ties to num 1, so char)
InitCharScript 0,255,0,0,0
;set up talk animation to be ID 2
SetCharFrame 0,2,2,1
;set up idle looping animation to be ID 0
SetCharFrame 0,0,0,1
;    load data and setup key characters done




;    setup room and key char positions
;teleport character
SetCharBox 0,11
;make character be hidden on screen
SetCharFlags 0,32768

;load room
LoadRoom 1

;check non-temporary flag to decide if hitbox description should be changed or not
Push
GetFlag 401
Push
FetchScriptWord 1
TestEquals
;if the flag is not set, then skip over the next few lines
Jz :skip-hitbox-1-set

;update the hitbox description same as in the actions to look/go through door
SetHitboxText 1

skip-hitbox-1-set:
;    setup room and key char positions done




;    start cutscene
; branch depending on if entry flag to program was 0 (new game), 1 (entered from other room)
Push
;get flag 0 (entry flag to program)
GetFlag 0
Push
;set value 0 (to compare against)
FetchScriptWord 0
;set current value to -1 if they're equal, or 0 if not
TestEquals
;if current value is not zero (so flag was 0) then jump to new game entry
Jnz :new-game
;this is where we would run new checks for other values if needed
;otherwise (flag was not 0) then jump to other room entry
Jz :other-room



new-game:
;a new game was started, so we want to animate the character coming into the room from the top door
;        animate background door opening
;wait a bit before starting animation
SetCharDelay 5

;draw the first area
UpdateRoomAreas 1
;wait a bit before next frame
SetCharDelay 10
;draw the next area
UpdateRoomAreas 2
;wait a bit before next frame
SetCharDelay 10
;draw the next area
UpdateRoomAreas 3
;wait a bit before next frame
SetCharDelay 10
;draw the next area
UpdateRoomAreas 4

;wait a bit before character shows up
SetCharDelay 10

;make character be shown on screen
UnsetCharFlags 0,32768

;start animating the door closing
UpdateRoomAreas 3

;dialogue
StartTalk 0,1
;wait for key char 0 of type -1 with IDs 0 (for character 0's animation to be 0 (idle, not speaking))
SetupWaitingChar 0,-1,0

;continue animating the door closing
UpdateRoomAreas 2

;slight delay on top
SetCharDelay 5
;dialogue
StartTalk 0,2
;wait for key char 0 of type -1 with IDs 0 (for character 0's animation to be 0 (idle, not speaking))
SetupWaitingChar 0,-1,0

;continue animating the door closing
UpdateRoomAreas 1

;slight delay on top
SetCharDelay 5

;finish animating the door closing
UpdateRoomAreas 5

;move character to another point (pathfinding)
MoveCharToPos 0,2,0
;wait for key char 0 of type 1 with ID 2 (for character 0's movement until moved to point 2)
SetupWaitingChar 0,1,2

;dialogue
StartTalk 0,3
;wait for key char 0 of type -1 with IDs 0 (for character 0's animation to be 0 (idle, not speaking))
SetupWaitingChar 0,-1,0
;jump to end of branch
Push
Jz :entry-done




other-room:
;the player was entering back into the room from a nearby one, so we want to animate the char coming from the right
;teleport character
SetCharBox 0,10
;face left
SetCharDirection 0,3
;make character be shown on screen
UnsetCharFlags 0,32768
;move character to another point (pathfinding)
MoveCharToPos 0,9,0
;wait for key char 0 of type 1 with ID 9 (for character 0's movement until moved to point 9)
SetupWaitingChar 0,1,9
;jump to end of branch
Push
Jz :entry-done





entry-done:
;    cutscene done

;    final setup
;show cursor
FetchScriptWord 0
SetFlag 618

;end
StopScript